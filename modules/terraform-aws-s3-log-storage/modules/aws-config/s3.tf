# ------------------------------------------------------------------------------
# S3 Log Storage Context
# ------------------------------------------------------------------------------
module "s3_log_storage_context" {
  source     = "SevenPico/context/null"
  version    = "2.0.0"
  context    = module.context.self
  attributes = ["aws-config-logs"]
}

locals {
  s3_log_storage_arn = format("arn:%s:s3:::%s", try(data.aws_partition.current[0].id, ""), module.s3_log_storage_context.id)
  s3_object_prefix   = format("%s/AWSLogs/*", local.s3_log_storage_arn)
}


# ------------------------------------------------------------------------------
# S3 Log StoragePolicy
# ------------------------------------------------------------------------------
data "aws_iam_policy_document" "s3_log_storage" {
  count = module.context.enabled ? 1 : 0
  #  source_policy_documents = var.s3_bucket_policy_source_json == "" ? [] : [var.s3_bucket_policy_source_json]

  statement {
    sid = "AWSConfigBucketPermissionsCheck"

    principals {
      type        = "Service"
      identifiers = ["config.amazonaws.com"]
    }

    effect  = "Allow"
    actions = ["s3:GetBucketAcl"]

    resources = [
      local.s3_log_storage_arn
    ]
    condition {
      test     = "StringEquals"
      variable = "aws:SourceAccount"
      values   = concat([local.account_id], var.source_accounts)
    }
    #    condition {
    #      test     = "ArnLike"
    #      variable = "aws:SourceArn"
    #      values   = concat(
    #        ["${local.arn_prefix}:logs:*:${local.account_id}:*"],
    #        [for account in var.source_accounts : "arn:aws:logs:*:${account}:*"]
    #      )
    #    }
  }

  statement {
    sid = "AWSConfigBucketExistenceCheck"

    principals {
      type        = "Service"
      identifiers = ["config.amazonaws.com"]
    }

    effect  = "Allow"
    actions = ["s3:ListBucket"]

    resources = [
      local.s3_log_storage_arn
    ]
    condition {
      test     = "StringEquals"
      variable = "aws:SourceAccount"
      values   = concat([local.account_id], var.source_accounts)
    }
    #    condition {
    #      test     = "ArnLike"
    #      variable = "aws:SourceArn"
    #      values   = concat(
    #        ["${local.arn_prefix}:logs:*:${local.account_id}:*"],
    #        [for account in var.source_accounts : "arn:aws:logs:*:${account}:*"]
    #      )
    #    }
  }

  statement {
    sid = "AWSConfigBucketDelivery"

    principals {
      type        = "Service"
      identifiers = ["config.amazonaws.com"]
    }

    effect  = "Allow"
    actions = ["s3:PutObject"]

    condition {
      test     = "StringLike"
      variable = "s3:x-amz-acl"
      values   = ["bucket-owner-full-control"]
    }
    condition {
      test     = "StringEquals"
      variable = "aws:SourceAccount"
      values   = concat([local.account_id], var.source_accounts)
    }
    #    condition {
    #      test     = "ArnLike"
    #      variable = "aws:SourceArn"
    #      values   = concat(
    #        ["${local.arn_prefix}:logs:*:${local.account_id}:*"],
    #        [for account in var.source_accounts : "arn:aws:logs:*:${account}:*"]
    #      )
    #    }

    resources = [local.s3_object_prefix]
  }
}


# ------------------------------------------------------------------------------
# S3 Bucket
# ------------------------------------------------------------------------------
module "s3_log_storage" {
  source  = "../../"
  context = module.s3_log_storage_context.self

  access_log_bucket_name            = var.access_log_bucket_name
  access_log_bucket_prefix_override = var.access_log_bucket_prefix_override
  acl                               = "log-delivery-write"
  allow_encrypted_uploads_only      = false
  allow_ssl_requests_only           = true
  block_public_acls                 = true
  block_public_policy               = true
  bucket_key_enabled                = false
  bucket_name                       = ""
  bucket_notifications_enabled      = false
  bucket_notifications_prefix       = ""
  bucket_notifications_type         = "SQS"
  enable_mfa_delete                 = var.enable_mfa_delete
  force_destroy                     = var.force_destroy
  ignore_public_acls                = true
  kms_master_key_arn                = module.kms_key.key_arn
  lifecycle_configuration_rules     = var.lifecycle_configuration_rules
  restrict_public_buckets           = true
  s3_object_ownership               = var.s3_object_ownership
  blocked_encryption_types          = var.blocked_encryption_types
  source_policy_documents           = concat([one(data.aws_iam_policy_document.s3_log_storage[*].json)], var.s3_source_policy_documents)
  sse_algorithm                     = module.kms_key.alias_arn == "" ? "AES256" : "aws:kms"
  enable_versioning                 = true
}
